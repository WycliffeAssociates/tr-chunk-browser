plugins {
    id "com.github.johnrengelman.shadow" version "5.2.0"
}

apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'edu.sc.seis.launch4j'

version = '0.2.0'

application {
    mainClassName = "org.bibletranslationtools.trchunkbrowser.cli.Main"
}

repositories {
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    implementation "io.reactivex.rxjava2:rxkotlin:$rxkotlinVer"
    implementation "com.github.thomasnield:rxkotlinfx:$rxkotlinfxVer"
    implementation "info.picocli:picocli:4.5.1"
    kapt "info.picocli:picocli-codegen:4.5.1"

    implementation project(':common')
}

kapt {
    arguments {
        arg("project", "${project.group}/${project.name}")
    }
}

// tell gradle what to put in the jar
shadowJar {
    archiveFileName = "${rootProject.name}-${project.name}.jar"
    mergeServiceFiles()
    manifest {
        attributes(
                'Main-Class': "org.bibletranslationtools.trchunkbrowser.cli.Main"
        )
    }
}

task copyJre(type: Copy) {
    if (project.hasProperty("jrePath")) {
        from jrePath
        into "${buildDir}/launch4j/jre"
    }
}

launch4j {
    outfile = "${rootProject.name}-cli.exe"
    mainClassName = "org.bibletranslationtools.trchunkbrowser.cli.Main"
    bundledJrePath = "jre"
    bundledJre64Bit = true
    jdkPreference = 'jdkOnly'
    jar = "${buildDir}/libs/${rootProject.name}-cli.jar"
    headerType = "console"
    productName = rootProject.name
}

project.tasks.createExe.dependsOn copyJre
project.tasks.createExe.dependsOn shadowJar
